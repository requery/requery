apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'maven-publish'

buildscript {
    ext.android_buildtools_version = '25.0.0'
    ext.jpa_version = '2.1.1'
    ext.kotlin_version = '1.0.6'
    ext.rxjava_version = '1.2.0'
    ext.rxjava2_version = '2.0.2'
    ext.reactor_version = '3.0.3.RELEASE'
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.2.0'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.4'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:0.9.8"
    }
}

allprojects {
    repositories {
        jcenter()
        maven { url 'http://oss.jfrog.org/artifactory/oss-snapshot-local' }
    }
}

idea {
    project {
        jdkName = '1.8'
    }
}

group = 'io.requery'
version = '1.1.0'
description = 'A light but powerful object mapper and SQL generator for Java/Android'

Properties properties = new Properties()
File localProperties = project.rootProject.file('local.properties')
if (localProperties.exists()) {
    properties.load(localProperties.newDataInputStream())
}

configure([project(':requery'), project(':requery-processor'), project(':requery-kotlin')]) {
    apply plugin: 'java'
    apply plugin: 'maven-publish'

    javadoc.options.links("http://docs.oracle.com/javase/8/docs/api/");

    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }

    publishing {
        publications {
            maven(MavenPublication) {
                from components.java
                groupId rootProject.group
                artifactId project.name
                version rootProject.version

                artifact sourcesJar
                artifact javadocJar

                pom.withXml {
                    asNode().children().last() + project.pomXml
                }
            }
        }

        repositories {
            maven {
                credentials {
                    username = bintray.user
                    password = bintray.key
                }
                url 'http://oss.jfrog.org/artifactory/oss-snapshot-local'
            }
        }
    }
}

configure([project(':requery'), project(':requery-processor'), project(':requery-android'), project(':requery-kotlin')]) {
    apply plugin: 'com.jfrog.bintray'

    bintray {
        user = properties.getProperty('bintray.user')
        key = properties.getProperty('bintray.apikey')
        publications = ['maven']
        pkg {
            repo = 'requery'
            name = 'requery'
            userOrg = 'requery'
            licenses = ['Apache-2.0']
            vcsUrl = 'https://github.com/requery/requery.git'
            version {
                name = rootProject.version
                desc = rootProject.description
                released  = new Date()
            }
        }
    }

    ext {
        pomXml = {
            resolveStrategy = Closure.DELEGATE_FIRST
            name project.name
            description project.description ?: rootProject.description
            url 'https://github.com/requery/requery'
            scm {
                url 'https://github.com/requery/requery.git'
                connection 'scm:git:git://github.com/requery/requery.git'
                developerConnection 'scm:git:git@github.com/requery/requery.git'
            }
            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/license/LICENSE-2.0.txt'
                    distribution 'repo'
                }
            }
            developers {
                developer {
                    id 'npurushe'
                    name 'Nikhil Purushe'
                }
            }
        }
    }
}
