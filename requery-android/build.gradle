buildscript {
    repositories {
        jcenter()
        mavenCentral()
    }
}

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'maven-publish'

android {
    compileSdkVersion 26
    buildToolsVersion "$android_buildtools_version"

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        consumerProguardFiles 'proguard-rules.pro'
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    lintOptions {
        abortOnError false
        disable 'InvalidPackage'
    }
    packagingOptions {
        exclude 'META-INF/rxjava.properties'
    }
}

dependencies {
    implementation project(':requery')
    implementation "com.android.support:support-v4:$android_support_version"
    implementation "com.android.support:recyclerview-v7:$android_support_version"
    implementation 'net.zetetic:android-database-sqlcipher:3.5.9'
    implementation 'io.requery:sqlite-android:3.21.0'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation project(':requery-test')
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test:rules:1.0.2'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
}

publish.dependsOn 'assembleRelease'
bintrayUpload.dependsOn 'assembleRelease'

android.libraryVariants
publishing {
    publications {
        maven(MavenPublication) {
            // leaving out dependencies (components.java) intentionally
            groupId rootProject.group
            artifactId project.name
            version rootProject.version
            artifact getReleaseArtifact()
            artifact sourcesJar
            artifact javadocJar
            pom.withXml {
                asNode().children().last() + project.pomXml
            }
        }
    }

    repositories {
        maven {
            credentials {
                username = bintray.user
                password = bintray.key
            }
            url 'http://oss.jfrog.org/artifactory/oss-snapshot-local'
        }
    }
}

def getReleaseArtifact() {
    return "build/outputs/aar/${project.name}-release.aar"
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    classpath += configurations.compile
    failOnError false
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
repositories {
    mavenCentral()
}
